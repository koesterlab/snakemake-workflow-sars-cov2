configfile: "config/config.yaml"


pepfile: config["pepfile"]


container: "docker://condaforge/mambaforge"


include: "rules/common.smk"
include: "rules/utils.smk"
include: "rules/ref.smk"
include: "rules/qc.smk"
include: "rules/read_trimming.smk"
include: "rules/assembly.smk"
include: "rules/strain_calling.smk"
include: "rules/read_mapping.smk"
include: "rules/variant_calling.smk"
include: "rules/variant_annotation.smk"
include: "rules/variant_filtration.smk"
include: "rules/variant_report.smk"
include: "rules/benchmarking.smk"


rule all:
    input:
        "results/qc/multiqc.html",
        "results/plots/strain-calls/all.strains.pangolin.svg",
        expand("results/vcf-report/{target}", target=["all"] + get_samples()),  # TODO activate once report properly handles intergenic variants.
        expand(
            "results/filtered-calls/ref~main/{sample}.subclonal.bcf",
            sample=get_samples(),
        ),
        expand(
            "results/plots/strain-calls/all.{mode}-strain.strains.kallisto.svg",
            mode=["major", "any"],
        ),
        expand(
            "results/tables/strain-calls/{sample}.strains.pangolin.csv",
            sample=get_samples(),
        ),
        expand(
            "results/plots/strain-calls/{sample}.strains.{method}.svg",
            sample=get_samples(),
            method=["kallisto", "pangolin"],
        ),
        expand("results/quast/{sample}", sample=get_samples()),
        expand("results/polished-contigs/{sample}.fasta", sample=get_samples()),


rule benchmark_strain_calling:
    input:
        "results/benchmarking/strain-calling.csv",


rule benchmark_assembly:
    input:
        "results/benchmarking/assembly.csv",
